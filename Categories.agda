{-# OPTIONS --type-in-type #-}

module Categories where

open import Prelude


----------------------------------------------------------------------------
-- Definition of a category
----------------------------------------------------------------------------

record Category : Set where
  infixl 3 _>~>_
  field
    -- two types of thing
    Obj  : Set                  -- "objects"
    _~>_ : Obj â†’ Obj â†’ Set      -- "arrows" or "morphisms"

    -- two operations
    id~>        : {T : Obj} â†’      T ~> T
    _>~>_       : {R S T : Obj} â†’  R ~> S â†’ S ~> T â†’ R ~> T

    -- Composition left unit law
    law-id~>Ë¡ : {S T : Obj}     (f : S ~> T) â†’ id~> >~> f â‰¡ f
    -- Composition right unit law
    law-id~>Ê³ : {S T : Obj}     (f : S ~> T) â†’ f >~> id~> â‰¡ f
    -- Composition associative law
    law->~>  : {Q R S T : Obj} (f : Q ~> R) (g : R ~> S)(h : S ~> T) â†’ (f >~> g) >~> h â‰¡ f >~> (g >~> h)


  -- The so-called whiskering
  whiskerË¡ : {A B C : Obj} {g1 g2 : B ~> C} {f : A ~> B} â†’ g1 â‰¡ g2 â†’ f >~> g1 â‰¡ f >~> g2
  whiskerË¡ {f = f} refl = refl

  whiskerÊ³ : {B C D : Obj} {g1 g2 : B ~> C} {h : C ~> D} â†’ g1 â‰¡ g2 â†’ g1 >~> h â‰¡ g2 >~> h
  whiskerÊ³ {h = h} refl = refl


----------------------------------------------------------------------------
-- Structured sets as categories
----------------------------------------------------------------------------

-- Empty category
EMPTY : Category
EMPTY = record
          { Obj = Zero
          ; _~>_ = Î» _ _ â†’ Zero
          ; id~> = Î» {T} â†’ T
          ; _>~>_ = Î» x _ â†’ magic x
          ; law-id~>Ë¡ = Î» f â†’ magic f
          ; law-id~>Ê³ = Î» f â†’ magic f
          ; law->~> = Î» f g h â†’ magic f
          }


-- Another trivial category
ONE : Category
ONE = record
        { Obj = One
        ; _~>_ = Î» _ _ â†’ One
        ; id~> = <>
        ; _>~>_ = Î» _ _ â†’ <>
        ; law-id~>Ë¡ = Î» { <> â†’ refl }
        ; law-id~>Ê³ = Î» { <> â†’ refl }
        ; law->~> = Î» _ _ _ â†’ refl
        }


record Preorder (X : Set) : Set where
  infixl 5 _â‰¤_
  field
    _â‰¤_ : X â†’ X â†’ Set
    â‰¤-refl : (x : X) â†’ x â‰¤ x
    â‰¤-trans : (x y z : X) â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
    â‰¤-unique : (x y : X) â†’ (p q : x â‰¤ y) â†’ p â‰¡ q
open Preorder {{...}} public


-- Preorder is a category
PREORDER : {X : Set} {{_ : Preorder X}} â†’ Category
PREORDER {X} = record
             { Obj = X
             ; _~>_ = _â‰¤_
             ; id~> = Î» {T} â†’ â‰¤-refl T
             ; _>~>_ = Î» {R} {S} {T} f g â†’ â‰¤-trans R S T f g
             ; law-id~>Ë¡ = Î» {S} {T} _ â†’ â‰¤-unique S T _ _
             ; law-id~>Ê³ = Î» {S} {T} _ â†’ â‰¤-unique S T _ _
             ; law->~> = Î» {Q} {R} {S} {T} f g h â†’ â‰¤-unique Q T _ _
             }


record Monoid (X : Set) : Set where
  infixl 5 _â‹†_
  field
    Îµ : X
    _â‹†_ : X â†’ X â†’ X
    absorbL : (x : X) â†’ Îµ â‹† x â‰¡ x
    absorbR : (x : X) â†’ x â‹† Îµ â‰¡ x
    assoc   : (x y z : X) â†’ (x â‹† y) â‹† z â‰¡ x â‹† (y â‹† z)
open Monoid {{...}} public


-- Monoid is a category
MONOID : {X : Set} {{_ : Monoid X}} â†’ Category
MONOID {X}
  =   record
       { Obj = One
       ; _~>_ = Î» _ _ â†’ X
       ; id~> = Îµ
       ; _>~>_ = _â‹†_
       ; law-id~>Ë¡ = absorbL
       ; law-id~>Ê³ = absorbR
       ; law->~> = assoc
       }


----------------------------------------------------------------------------
-- Categories of structured sets
----------------------------------------------------------------------------

-- The category of sets
SET : Category
SET = record
        { Obj = Set
        ; _~>_ = Î» S T â†’ S â†’ T
        ; id~> = id
        ; _>~>_ = _>>_
        ; law-id~>Ë¡ = Î» _ â†’ refl
        ; law-id~>Ê³ = Î» _ â†’ refl
        ; law->~> = Î» _ _ _ â†’ refl
        }

-- Monotone
record Monotone {X} {{MX : Preorder X}} {Y} {{MY : Preorder Y}} (f : X â†’ Y) : Set where
  field
    respâ‰¤ : âˆ€ {x x'} â†’ x â‰¤ x' â†’ f x â‰¤ f x'


SomePreorder : Set
SomePreorder = Î£ Set Preorder


-- The category of preorders
Cat-Preorder : Category
Cat-Preorder = record
             { Obj = SomePreorder
             ; _~>_ = Î» { (m , â‰¤m) (n , â‰¤n) â†’ Subset (m â†’ n) Î» f â†’ Monotone {{_}} {{_}} f }
             ; id~> = id # record { respâ‰¤ = id }
             ; _>~>_ = Î» { {R , m} {S , n} {T , s} (f # fm) (g # gm) â†’
                    let instance
                           -- Bring instances into scope
                           _ : Preorder S
                           _ = n
                           _ : Preorder R
                           _ = m
                           _ : Preorder T
                           _ = s
                     in f >> g # record { respâ‰¤ = Î» xâ‰¤y â†’ Monotone.respâ‰¤ gm (Monotone.respâ‰¤ fm xâ‰¤y) }

               }
             ; law-id~>Ë¡ = Î» _ â†’ refl
             ; law-id~>Ê³ = Î» _ â†’ refl
             ; law->~> = Î» _ _ _ â†’ refl
             }


-- Monoid homomorphism
record MonoidHom {X} {{MX : Monoid X}} {Y} {{MY : Monoid Y}} (f : X  â†’ Y) : Set where
  field
    respÎµ : f Îµ â‰¡ Îµ
    respâ‹† : âˆ€ x x' â†’ f (x â‹† x') â‰¡ f x â‹† f x'


SomeMonoid : Set
SomeMonoid = Î£ Set Monoid

-- The category of monoids
CAT-MONOID : Category
CAT-MONOID  = record
               { Obj = SomeMonoid
               ; _~>_ = Î» { (m , â‹†m) (n , â‹†n) â†’ Subset (m â†’ n) Î» f â†’ MonoidHom {{_}} {{_}} f  }
               ; id~> = id # record { respÎµ = refl ; respâ‹† = Î» _ _ â†’ refl }
               ; _>~>_ = Î» { {R , m} {S , n} {T , s} (f # fm) (g # gm) â†’
                       let instance
                             _ : Monoid S
                             _ = n
                             _ : Monoid R
                             _ = m
                             _ : Monoid T
                             _ = s
                       in
                       f >> g # record { respÎµ = g (f Îµ)    â‰¡âŸ¨ cong g (MonoidHom.respÎµ fm) âŸ©
                                                 g Îµ        â‰¡âŸ¨ MonoidHom.respÎµ gm âŸ©
                                                 Îµ
                                                 â–¡
                                       ; respâ‹† = Î» a b â†’ g (f (a â‹† b))     â‰¡âŸ¨ cong g (MonoidHom.respâ‹† fm a b) âŸ©
                                                         g (f a â‹† f b)     â‰¡âŸ¨ MonoidHom.respâ‹† gm (f a) (f b) âŸ©
                                                         g (f a) â‹† g (f b)
                                                         â–¡
                                       }
                        }
               ; law-id~>Ë¡ = Î» _ â†’ refl
               ; law-id~>Ê³ = Î» _ â†’ refl
               ; law->~> = Î» _ _ _ â†’ refl
               }


----------------------------------------------------------------------------
-- Categories of categories
----------------------------------------------------------------------------

module FUNCTOR where
  open Category
  -- Functor from C to D
  record _=>_ (C D : Category) : Set where
    field
      -- Two mappings
      ğ”½â‚€ : Obj C â†’ Obj D
      ğ”½â‚ : {S T : Obj C} â†’ _~>_ C S T â†’ _~>_ D (ğ”½â‚€ S) (ğ”½â‚€ T)

      -- Two laws
      F-map-id~> : {T : Obj C} â†’ ğ”½â‚ (id~> C {T}) â‰¡ id~> D {ğ”½â‚€ T}
      F-map->~> : {R S T : Obj C} (f : _~>_ C R S) (g : _~>_ C S T) â†’
                  ğ”½â‚ (_>~>_ C f g) â‰¡ _>~>_ D (ğ”½â‚ f) (ğ”½â‚ g)

open FUNCTOR public


-- Identity functor
Functor-id : âˆ€ {C} â†’ C => C
Functor-id = record { ğ”½â‚€ = id ; ğ”½â‚ = id ; F-map-id~> = refl ; F-map->~> = Î» _ _ â†’ refl }


-- Functor composition
module FUNCTOR-CP {C D E : Category} where
  open _=>_
  open Category

  _>=>_ : C => D â†’ D => E â†’ C => E
  ğ”½â‚€ (F >=> G) = ğ”½â‚€ F >> ğ”½â‚€ G
  ğ”½â‚ (F >=> G) = ğ”½â‚ F >> ğ”½â‚ G
  F-map-id~> (F >=> G) = ğ”½â‚ G (ğ”½â‚ F (id~> C))         â‰¡âŸ¨ cong (ğ”½â‚ G) (F-map-id~> F) âŸ©
                         ğ”½â‚ G (id~> D)                â‰¡âŸ¨ F-map-id~> G âŸ©
                         id~> E
                         â–¡
  F-map->~> (F >=> G) f g =  ğ”½â‚ G (ğ”½â‚ F (_>~>_ C f g))                      â‰¡âŸ¨ cong (ğ”½â‚ G) (F-map->~> F f g) âŸ©
                             ğ”½â‚ G (_>~>_ D (ğ”½â‚ F f) (ğ”½â‚ F g))               â‰¡âŸ¨ F-map->~> G (ğ”½â‚ F f) (ğ”½â‚ F g) âŸ©
                             _>~>_ E (ğ”½â‚ G (ğ”½â‚ F f)) (ğ”½â‚ G (ğ”½â‚ F g))
                             â–¡


open FUNCTOR-CP public


-- Functor (extensional) equivalence:
-- ğ”½â‚€ â‰¡ ğ”¾â‚€
-- ğ”½â‚ â‰¡ ğ”¾â‚
-- F-map-id~> â‰¡ G-map-id~>
-- F-map->~> â‰¡ G-map->~>
Functorâ‰¡ : {C D : Category} (F G : C => D) â†’ Set
Functorâ‰¡ {C} {D}
  record { ğ”½â‚€ = ğ”½â‚€ ; ğ”½â‚ = ğ”½â‚ ; F-map-id~> = F-map-id~> ; F-map->~> = F-map->~> }
  record { ğ”½â‚€ = ğ”¾â‚€ ; ğ”½â‚ = ğ”¾â‚ ; F-map-id~> = G-map-id~> ; F-map->~> = G-map->~> }
  = Î£ (ğ”½â‚€ â‰¡ ğ”¾â‚€)
       Î» { refl  â†’ -- Patterns lambdas
         Î£ (_â‰¡_ {âˆ€ {S T : Category.Obj C} â†’ (C Category.~> S) T â†’ (D Category.~> ğ”½â‚€ S) (ğ”¾â‚€ T)} ğ”½â‚ ğ”¾â‚)
            Î» { refl â†’
                _â‰¡_ {forall {T : Category.Obj C} â†’ ğ”½â‚ (Category.id~> C {T}) â‰¡ Category.id~> D} F-map-id~> G-map-id~>
                Ã—
                _â‰¡_ {forall {R S T : Category.Obj C} (f : (C Category.~> R) S) (g : (C Category.~> S) T) â†’
                     ğ”½â‚ ((C Category.>~> f) g) â‰¡ (D Category.>~> ğ”½â‚ f) (ğ”½â‚ g)}
                     F-map->~>  G-map->~>
              }
         }

-- Functor equivalence implies propositional equivalence
Functorâ‰¡â†’â‰¡ : {C D : Category}{F G : C => D} â†’ Functorâ‰¡ F G â†’ F â‰¡ G
Functorâ‰¡â†’â‰¡ (refl , (refl , (refl , refl)))  = refl


-- The category of categories
CATEGORY : Category
CATEGORY = record
             { Obj = Category
             ; _~>_ =  _=>_
             ; id~> = Functor-id
             ; _>~>_ = _>=>_
             ; law-id~>Ë¡ =
               Î» F â†’ Functorâ‰¡â†’â‰¡ (refl , refl ,
                 extensionality' (Î» x â†’ eqUnique _ _) ,
                 extensionality' Î» x â†’
                   extensionality' Î» y â†’
                     extensionality' Î» z â†’
                       extensionality Î» g â†’ extensionality Î» h â†’ eqUnique _ _)
             ; law-id~>Ê³ =
               Î» F â†’ Functorâ‰¡â†’â‰¡ (refl , refl ,
                 extensionality' (Î» x â†’ eqUnique _ _) ,
                   extensionality' Î» x â†’
                     extensionality' Î» y â†’
                       extensionality' Î» z â†’ extensionality Î» g â†’ extensionality Î» h â†’ eqUnique _ _)
             ; law->~> =
               Î» F G H â†’ Functorâ‰¡â†’â‰¡ (refl , refl ,
                 extensionality' (Î» x â†’ eqUnique _ _) ,
                   extensionality' Î» x â†’
                     extensionality' Î» y â†’
                       extensionality' Î» z â†’
                         extensionality Î» g â†’ extensionality Î» h â†’ eqUnique _ _)
             } where open _=>_


-- A forgetful functor
U : âˆ€ {X} {{m : Monoid X}} â†’ MONOID {{m}} => SET
U {X} = record { ğ”½â‚€ = Î» _ â†’ X
               ; ğ”½â‚ = Î» x y â†’  y â‹† x
               ; F-map-id~> = extensionality absorbR
               ; F-map->~> = Î» x y â†’ extensionality Î» z â†’ sym (assoc z x y)
               }


-- A representable functor
module Rep (C : Category) where
  open Category C

  â„‚â‚“ : (X : Obj) â†’ C => SET
  â„‚â‚“ X = record { ğ”½â‚€ = Î» A â†’ X ~> A  ; ğ”½â‚ = Î» f g â†’ g >~> f
                ; F-map-id~> = extensionality Î» x â†’ law-id~>Ê³ _
                ; F-map->~> = Î» f g â†’ extensionality Î» x â†’ sym (law->~> x f g)
                }


----------------------------------------------------------------------------
-- New categories from old
----------------------------------------------------------------------------

-- Opposite categories
_op : Category â†’ Category
C op = record
         { Obj = Obj
         ; _~>_ = Î» x y â†’ y ~> x
         ; id~> = id~>
         ; _>~>_ = Î» f g â†’ g >~> f
         ; law-id~>Ë¡ = Î» f â†’ law-id~>Ê³ f
         ; law-id~>Ê³ = Î» f â†’ law-id~>Ë¡ f
         ; law->~> = Î» f g h â†’ sym (law->~> h g f)
         } where open Category C


-- Arrow categories
module ArrowCat (C : Category) where
  open Category C

  record ArrowObj : Set where
    constructor arrobj
    field
      {A} : Obj
      {B} : Obj
      arr : A ~> B

  record Arrow~> (X Y : ArrowObj) : Set where
    constructor arrarr
    module X = ArrowObj X
    module Y = ArrowObj Y
    f : X.A ~> X.B
    f = X.arr
    g : Y.A ~> Y.B
    g = Y.arr
    field
      i : X.A ~> Y.A
      j : X.B ~> Y.B
      .commuteSquare : i >~> g â‰¡ f >~> j


  Arrow~>-â‰¡ : âˆ€ {X Y} â†’ {f g : Arrow~> X Y} â†’ Arrow~>.i f â‰¡ Arrow~>.i g â†’ Arrow~>.j f â‰¡ Arrow~>.j g â†’ f â‰¡ g
  Arrow~>-â‰¡ {f = arrarr _ _ _} {arrarr _ _ _} eq1 eq2 rewrite eq1 | eq2 = refl

  arrow : Category
  arrow = record
            { Obj = ArrowObj
            ; _~>_ = Arrow~>
            ; id~> = Î» { {arrobj {A} {B} f} â†’
                   arrarr (id~> {A}) (id~> {B})
                          ( id~> >~> f            â‰¡âŸ¨ law-id~>Ë¡ _ âŸ©
                            f                      âŸ¨ law-id~>Ê³ _ âŸ©â‰¡
                            f >~> id~>
                            â–¡
                          )
                   }
            ; _>~>_ = Î» { {arrobj {A} {B} f} {arrobj {C} {D} g} {arrobj {E} {F} h} ij kl â†’
                    let i : A ~> C
                        i = Arrow~>.i ij
                        j : B ~> D
                        j = Arrow~>.j ij
                        k : C ~> E
                        k = Arrow~>.i kl
                        l : D ~> F
                        l = Arrow~>.j kl
                    in arrarr (i >~> k) (j >~> l)
                              ( i >~> k >~> h                â‰¡âŸ¨ law->~> i k h âŸ©
                                i >~> (k >~> h)              â‰¡âŸ¨ whiskerË¡ (Arrow~>.commuteSquare kl) âŸ©
                                i >~> (g >~> l)               âŸ¨ law->~> i g l âŸ©â‰¡
                                i >~> g >~> l                â‰¡âŸ¨ whiskerÊ³ (Arrow~>.commuteSquare ij) âŸ©
                                f >~> j >~> l                â‰¡âŸ¨ law->~> f j l âŸ©
                                f >~> (j >~> l)
                                â–¡
                              )

                    }
            ; law-id~>Ë¡ = Î» f â†’ Arrow~>-â‰¡ (law-id~>Ë¡ _) (law-id~>Ë¡ _)
            ; law-id~>Ê³ = Î» f â†’ Arrow~>-â‰¡ (law-id~>Ê³ _) (law-id~>Ê³ _)
            ; law->~> = Î» f g h â†’ Arrow~>-â‰¡ (law->~> _ _ _) (law->~> _ _ _)
            }


  -- Domain functor
  dom-functor : arrow => C
  dom-functor = record { ğ”½â‚€ = ArrowObj.A ; ğ”½â‚ = Arrow~>.i ; F-map-id~> = refl ; F-map->~> = Î» _ _ â†’ refl }

  -- reflexivity functor
  refl-functor : C => arrow
  refl-functor =
    record { ğ”½â‚€ = Î» x â†’ arrobj (id~> {x})
           ; ğ”½â‚ = Î» x â†’ arrarr x x (x >~> id~> â‰¡âŸ¨ law-id~>Ê³ x âŸ© x âŸ¨ law-id~>Ë¡ x âŸ©â‰¡ id~> >~> x â–¡)
           ; F-map-id~> = refl
           ; F-map->~> = Î» f g â†’ refl
           }

  -- codomain functor
  cod-functor : arrow => C
  cod-functor = record { ğ”½â‚€ = ArrowObj.B ; ğ”½â‚ = Arrow~>.j ; F-map-id~> = refl ; F-map->~> = Î» _ _ â†’ refl }



-- Slice categories
module SliceCat (C : Category) (A : Category.Obj C) where
  open Category C

  record SliceObj : Set where
    constructor sliceobj
    field
      {B} : Obj
      arr : B ~> A


  record Slice~> (X Y : SliceObj) : Set where
    constructor slicearr
    module X = SliceObj X
    module Y = SliceObj Y
    field
      p : X.B ~> Y.B
      .commuteTri : p >~> Y.arr â‰¡ X.arr

  Arrow~>-â‰¡ : âˆ€ {X Y} â†’ {f g : Slice~> X Y} â†’ Slice~>.p f â‰¡ Slice~>.p g â†’ f â‰¡ g
  Arrow~>-â‰¡ {f = slicearr _ _} {g = slicearr _ _} eq rewrite eq  = refl

  slice : Category
  slice = record
            { Obj = SliceObj
            ; _~>_ = Slice~>
            ; id~> = slicearr id~> (law-id~>Ë¡ _)
            ; _>~>_ =
              Î» { {sliceobj r} {sliceobj s} {sliceobj t} (slicearr f f-prf) (slicearr g g-prf) â†’
                  slicearr (f >~> g)
                         ( f >~> g >~> t           â‰¡âŸ¨ law->~> _ _ _ âŸ©
                           f >~> (g >~> t)         â‰¡âŸ¨ whiskerË¡ g-prf âŸ©
                           f >~> s                 â‰¡âŸ¨ f-prf âŸ©
                           r
                           â–¡
                         )
                }
            ; law-id~>Ë¡ = Î» _ â†’ Arrow~>-â‰¡ (law-id~>Ë¡ _)
            ; law-id~>Ê³ = Î» _ â†’ Arrow~>-â‰¡ (law-id~>Ê³ _)
            ; law->~> = Î» _ _ _ â†’ Arrow~>-â‰¡ (law->~> _ _ _)
            }


module Post-Composition-Functor {C : Category} {A B : Category.Obj C} (f : Category._~>_ C A B) where
  open Category C
  module C/A = SliceCat C A
  module C/B = SliceCat C B

  f! : C/A.slice => C/B.slice
  f! = record { ğ”½â‚€ = Î» { (C/A.SliceObj.sliceobj x) â†’ C/B.SliceObj.sliceobj (x >~> f) }
              ; ğ”½â‚ = Î» { {C/A.SliceObj.sliceobj x} {C/A.SliceObj.sliceobj y}  (C/A.Slice~>.slicearr p p-prf) â†’
                          C/B.slicearr p ( p >~> (y >~> f)       âŸ¨ law->~> _ _ _ âŸ©â‰¡
                                           p >~> y >~> f        â‰¡âŸ¨ whiskerÊ³ p-prf âŸ©
                                           x >~> f
                                           â–¡
                                         )
                       }
              ; F-map-id~> = refl
              ; F-map->~> = Î» _ _ â†’ refl }


----------------------------------------------------------------------------
-- Monic and epic morphisms
----------------------------------------------------------------------------

module Monic-Epic {C : Category} where
  open Category C

  Monic : {A B : Obj} (â†£ : A ~> B) â†’ Set
  Monic {A} {B} â†£ = âˆ€ {C} {f g : C ~> A} â†’ f >~> â†£ â‰¡ g >~> â†£ â†’ f â‰¡ g


  Epic : {A B : Obj} (â†  : A ~> B) â†’ Set
  Epic {A} {B} â†  = âˆ€ {C} {f g : B ~> C} â†’ â†  >~> f â‰¡ â†  >~> g â†’ f â‰¡ g

  id-monic : âˆ€ {T} â†’ Monic (id~> {T})
  id-monic {f = f} {g = g} post = f              âŸ¨ law-id~>Ê³ _ âŸ©â‰¡
                                  f >~> id~>    â‰¡âŸ¨ post âŸ©
                                  g >~> id~>    â‰¡âŸ¨ law-id~>Ê³ _ âŸ©
                                  g
                                  â–¡

  id-epic : âˆ€ {T} â†’ Epic (id~> {T})
  id-epic {f = f} {g = g} pre = f              âŸ¨ law-id~>Ë¡ _ âŸ©â‰¡
                                id~> >~> f    â‰¡âŸ¨ pre âŸ©
                                id~> >~> g    â‰¡âŸ¨ law-id~>Ë¡ _ âŸ©
                                g
                                â–¡

  >~>-monic : âˆ€ {A B C} {m : A ~> B} {n : B ~> C} â†’ Monic m â†’ Monic n â†’ Monic (m >~> n)
  >~>-monic {m = m} {n = n}  â†£m â†£n {f = f} {g = g} post = â†£m (â†£n help)
    where help : f >~> m >~> n â‰¡ g >~> m >~> n
          help = f >~> m >~> n        â‰¡âŸ¨ law->~> _ _ _ âŸ©
                 f >~> (m >~> n)      â‰¡âŸ¨ post âŸ©
                 g >~> (m >~> n)       âŸ¨ law->~> _ _ _ âŸ©â‰¡
                 g >~> m >~> n
                 â–¡

  >~>-epic : âˆ€ {A B C} {m : A ~> B} {n : B ~> C} â†’ Epic m â†’ Epic n â†’ Epic (m >~> n)
  >~>-epic {m = m} {n = n}  â† m â† n {f = f} {g = g} pre = â† n (â† m help)
    where help : m >~> (n >~> f) â‰¡ m >~> (n >~> g)
          help = m >~> (n >~> f) âŸ¨ law->~> _ _ _ âŸ©â‰¡
                 m >~> n >~> f â‰¡âŸ¨ pre âŸ©
                 m >~> n >~> g â‰¡âŸ¨ law->~> _ _ _ âŸ©
                 m >~> (n >~> g)
                 â–¡

  >~>-monicÊ³ : âˆ€ {A B C} {m : A ~> B} {n : B ~> C} â†’ Monic (m >~> n) â†’ Monic m
  >~>-monicÊ³ {m = m} {n = n} â†£mn {f = f} {g = g} post = â†£mn help
    where help : f >~> (m >~> n) â‰¡ g >~> (m >~> n)
          help = f >~> (m >~> n) âŸ¨ law->~> _ _ _ âŸ©â‰¡
                 f >~> m >~> n â‰¡âŸ¨ whiskerÊ³ post âŸ©
                 g >~> m >~> n â‰¡âŸ¨ law->~> _ _ _ âŸ©
                 g >~> (m >~> n)
                 â–¡

  >~>-epicÊ³ : âˆ€ {A B C} {m : A ~> B} {n : B ~> C} â†’ Epic (m >~> n) â†’ Epic n
  >~>-epicÊ³ {m = m} {n = n} â† mn {f = f} {g = g} pre = â† mn help
    where help : m >~> n >~> f â‰¡ m >~> n >~> g
          help = m >~> n >~> f â‰¡âŸ¨ law->~> _ _ _ âŸ©
                 m >~> (n >~> f) â‰¡âŸ¨ whiskerË¡ pre âŸ©
                 m >~> (n >~> g) âŸ¨ law->~> _ _ _ âŸ©â‰¡
                 m >~> n >~> g
                 â–¡



----------------------------------------------------------------------------
-- Split monic and epic morphisms
----------------------------------------------------------------------------


module Split-Monic-Epic {C : Category} where
  open Category C
  open Monic-Epic {C}

  record Split-Monic {A B : Obj} (s : A ~> B) : Set where
    field
      r : B ~> A
      post-invert : s >~> r â‰¡ id~>

  record Split-Epic {A B : Obj} (r : A ~> B) : Set where
    field
      s : B ~> A
      pre-invert : s >~> r â‰¡ id~>

  split-monic : {A B : Obj} {s : A ~> B} â†’ Split-Monic s â†’ Monic s
  split-monic {A} {B} {s} m {f = f} {g = g} post =
      f                         âŸ¨ law-id~>Ê³ _ âŸ©â‰¡
      f >~> id~>               â‰¡âŸ¨ cong (Î» x â†’ f >~> x) (sym post-invert) âŸ©
      f >~> (s >~> r)           âŸ¨ law->~> _ _ _ âŸ©â‰¡
      f >~> s >~> r            â‰¡âŸ¨ whiskerÊ³ post âŸ©
      g >~> s >~> r            â‰¡âŸ¨ law->~> _ _ _ âŸ©
      g >~> (s >~> r)          â‰¡âŸ¨ cong (Î» x â†’ g >~> x) post-invert âŸ©
      g >~> id~>               â‰¡âŸ¨ law-id~>Ê³ _ âŸ©
      g
      â–¡
    where open Split-Monic m


  split-epic : {A B : Obj} {r : A ~> B} â†’ Split-Epic r â†’ Epic r
  split-epic {A} {B} {r} m {f = f} {g = g} pre =
      f                         âŸ¨ law-id~>Ë¡ _ âŸ©â‰¡
      id~> >~> f               â‰¡âŸ¨ cong (Î» x â†’ x >~> f) (sym pre-invert) âŸ©
      s >~> r >~> f            â‰¡âŸ¨ law->~> _ _ _ âŸ©
      s >~> (r >~> f)          â‰¡âŸ¨ whiskerË¡ pre âŸ©
      s >~> (r >~> g)           âŸ¨ law->~> _ _ _ âŸ©â‰¡
      s >~> r >~> g            â‰¡âŸ¨ cong (Î» x â†’ x >~> g) pre-invert âŸ©
      id~> >~> g               â‰¡âŸ¨ law-id~>Ë¡ _ âŸ©
      g
      â–¡
    where open Split-Epic m


module Functor-Split-Monic-Epic {C D : Category} (F : C => D)where
  open Category
  open Split-Monic-Epic
  open _=>_ F

  F-split-monic : {A B : Obj C} {s : _~>_ C A B} â†’
                  Split-Monic {C} s â†’
                  Split-Monic {D} (ğ”½â‚ s)
  F-split-monic {A} {B} {s} m =
    record { r = ğ”½â‚ r
           ; post-invert =  _>~>_ D (ğ”½â‚ s) (ğ”½â‚ r)     âŸ¨ F-map->~> s r âŸ©â‰¡
                            ğ”½â‚ (_>~>_ C s r)         â‰¡âŸ¨ cong (Î» x â†’ ğ”½â‚ x) post-invert âŸ©
                            ğ”½â‚ (id~> C)              â‰¡âŸ¨ F-map-id~> âŸ©
                            id~> D
                            â–¡
           }
    where open Split-Monic m

  F-split-epic : {A B : Obj C} {r : _~>_ C A B} â†’
                  Split-Epic {C} r â†’
                  Split-Epic {D} (ğ”½â‚ r)
  F-split-epic {A} {B} {r} m =
    record { s = ğ”½â‚ s
           ; pre-invert =  _>~>_ D (ğ”½â‚ s) (ğ”½â‚ r)     âŸ¨ F-map->~> s r âŸ©â‰¡
                            ğ”½â‚ (_>~>_ C s r)         â‰¡âŸ¨ cong (Î» x â†’ ğ”½â‚ x) pre-invert âŸ©
                            ğ”½â‚ (id~> C)              â‰¡âŸ¨ F-map-id~> âŸ©
                            id~> D
                            â–¡
           }
    where open Split-Epic m


----------------------------------------------------------------------------
-- Isomorphisms
----------------------------------------------------------------------------


module Iso {C : Category} where
  open Category C

  record have-section {A B : Obj} (r : B ~> A) : Set where
    field
      s : A ~> B
      section : s >~> r â‰¡ id~>

  record have-retraction {A B : Obj} (s : A ~> B) : Set where
    field
      r : B ~> A
      retraction : s >~> r â‰¡ id~>

  secâ‰¡retrac : {A B : Obj} {f : A ~> B}
               {s : have-section f} {r : have-retraction f} â†’
               have-section.s s â‰¡ have-retraction.r r
  secâ‰¡retrac {f = f} {record { s = s ; section = section }}
                     {record { r = r ; retraction = retraction }} =
             s                âŸ¨ law-id~>Ê³ _ âŸ©â‰¡
             s >~> id~>       âŸ¨ whiskerË¡ retraction âŸ©â‰¡
             s >~> (f >~> r)  âŸ¨ law->~> _ _ _ âŸ©â‰¡
             s >~> f >~> r   â‰¡âŸ¨ whiskerÊ³ section âŸ©
             id~> >~> r      â‰¡âŸ¨ law-id~>Ë¡ _ âŸ©
             r
             â–¡

  record isomorphism {A B : Obj} (f : A ~> B) : Set where
    field
      fÊ³ : B ~> A
      inverse  : f >~> fÊ³ â‰¡ id~>
      inverseÊ³ : fÊ³ >~> f â‰¡ id~>


  record _â‰…_ (A B : Obj) : Set where
    field
      f : A ~> B
      iso-witness : isomorphism f

  iso-refl : {A : Obj} â†’ A â‰… A
  iso-refl = record { f = id~> ; iso-witness = record { fÊ³ = id~> ; inverse = law-id~>Ê³ _ ; inverseÊ³ = law-id~>Ê³ _ } }

  iso-sym : {A B : Obj} â†’ A â‰… B â†’ B â‰… A
  iso-sym record { f = f ; iso-witness = record { fÊ³ = fÊ³ ; inverse = inverse ; inverseÊ³ = inverseÊ³ } } =
    record { f = fÊ³ ; iso-witness = record { fÊ³ = f ; inverse = inverseÊ³ ; inverseÊ³ = inverse } }

  iso-trans : {A B C : Obj} â†’ A â‰… B â†’ B â‰… C â†’ A â‰… C
  iso-trans record { f = fâ‚ ; iso-witness = record { fÊ³ = fÊ³â‚ ; inverse = inverseâ‚ ; inverseÊ³ = inverseÊ³â‚ } }
            record { f = fâ‚‚ ; iso-witness = record { fÊ³ = fÊ³â‚‚ ; inverse = inverseâ‚‚ ; inverseÊ³ = inverseÊ³â‚‚ } } =
     record { f = fâ‚ >~> fâ‚‚
            ; iso-witness = record { fÊ³ = fÊ³â‚‚ >~> fÊ³â‚
                                   ; inverse = fâ‚ >~> fâ‚‚ >~> (fÊ³â‚‚ >~> fÊ³â‚)   â‰¡âŸ¨ law->~> _ _ _ âŸ©
                                               fâ‚ >~> (fâ‚‚ >~> (fÊ³â‚‚ >~> fÊ³â‚)) â‰¡âŸ¨ whiskerË¡ (sym (law->~> _ _ _)) âŸ©
                                               fâ‚ >~> (fâ‚‚ >~> fÊ³â‚‚ >~> fÊ³â‚)   â‰¡âŸ¨ cong (Î» x â†’ fâ‚ >~> (x >~> fÊ³â‚)) inverseâ‚‚ âŸ©
                                               fâ‚ >~> (id~> >~> fÊ³â‚)         â‰¡âŸ¨ whiskerË¡ (law-id~>Ë¡ _) âŸ©
                                               fâ‚ >~> fÊ³â‚                    â‰¡âŸ¨ inverseâ‚ âŸ©
                                               id~>
                                               â–¡
                                   ; inverseÊ³ = fÊ³â‚‚ >~> fÊ³â‚ >~> (fâ‚ >~> fâ‚‚)    â‰¡âŸ¨ law->~> _ _ _ âŸ©
                                                fÊ³â‚‚ >~> (fÊ³â‚ >~> (fâ‚ >~> fâ‚‚))  â‰¡âŸ¨ whiskerË¡ (sym (law->~> _ _ _)) âŸ©
                                                fÊ³â‚‚ >~> (fÊ³â‚ >~> fâ‚ >~> fâ‚‚)    â‰¡âŸ¨ cong (Î» x â†’ fÊ³â‚‚ >~> (x >~> fâ‚‚)) inverseÊ³â‚ âŸ©
                                                fÊ³â‚‚ >~> (id~> >~> fâ‚‚)          â‰¡âŸ¨ whiskerË¡ (law-id~>Ë¡ _) âŸ©
                                                fÊ³â‚‚ >~> fâ‚‚                     â‰¡âŸ¨ inverseÊ³â‚‚ âŸ©
                                                id~>
                                                â–¡
                                   }
            }



----------------------------------------------------------------------------
-- Terminal and initial objects
----------------------------------------------------------------------------

record Terminal (C : Category): Set where
  open Category C
  field
    ğŸ™ : Obj
    ! : âˆ€ {A} â†’ A ~> ğŸ™
    !-unique : âˆ€ {A} â†’ (f : A ~> ğŸ™) â†’ f â‰¡ !

  -- identity expansion for terminals
  âŠ¤-id : (f : ğŸ™ ~> ğŸ™) â†’ f â‰¡ id~>
  âŠ¤-id f = f    â‰¡âŸ¨ !-unique f âŸ©
           !     âŸ¨ !-unique id~> âŸ©â‰¡
           id~>
           â–¡

module terminals-up-to-iso {C : Category} {T R : Terminal C} where

  open Category C
  open Terminal
  open Iso {C}

  up-to-iso : (ğŸ™ T) â‰… (ğŸ™ R)
  up-to-iso = record { f = ! R  ; iso-witness = record { fÊ³ = ! T ; inverse = âŠ¤-id T _ ; inverseÊ³ = âŠ¤-id R _ } }



module pre-composing-with-bang {C : Category} {T : Terminal C} where
  open Category C
  open Terminal

  lemma : {X Y : Obj} â†’ (i : Y ~> X) â†’ i >~> ! T â‰¡ ! T
  lemma i = !-unique T (i >~> ! T)


Terminal-Cat-Preorder : Terminal Cat-Preorder
Terminal-Cat-Preorder =
  record { ğŸ™ = One , record { _â‰¤_ = Î» _ _ â†’ One
                            ; â‰¤-refl = id
                            ; â‰¤-trans = Î» _ _ _ _ _ â†’ <>
                            ; â‰¤-unique = Î» _ _ _ _ â†’ refl }
         ; ! = (Î» _ â†’ <>) # record { respâ‰¤ = Î» _ â†’ <> }
         ; !-unique = Î» f â†’ refl }

record Increasing-Preorder {X} (p : Preorder X) : Set where
  open Preorder
  field
    e : X
    â‰¤-e : (x : X) â†’ _â‰¤_ p x e


Terminal-Preorder : âˆ€ {X} â†’ {{p : Preorder X}} (MP : Increasing-Preorder p) â†’ Terminal (PREORDER {{p}})
Terminal-Preorder MP = record { ğŸ™ = e MP ; ! = Î» {x} â†’ â‰¤-e MP x ; !-unique = Î» {x} _ â†’ â‰¤-unique x _ _ _ }
  where open Increasing-Preorder


record Initial (C : Category): Set where
  open Category C
  field
    ğŸ˜ : Obj
    ! : âˆ€ {A} â†’ ğŸ˜ ~> A
    !-unique : âˆ€ {A} â†’ (f : ğŸ˜ ~> A) â†’ f â‰¡ !

  -- identity expansion for initials
  âŠ¥-id : (f : ğŸ˜ ~> ğŸ˜) â†’ f â‰¡ id~>
  âŠ¥-id f = f    â‰¡âŸ¨ !-unique f âŸ©
           !     âŸ¨ !-unique id~> âŸ©â‰¡
           id~>
           â–¡

module initials-up-to-iso {C : Category} {T R : Initial C} where

  open Category C
  open Initial
  open Iso {C}

  up-to-iso : (ğŸ˜ T) â‰… (ğŸ˜ R)
  up-to-iso = record { f = ! T ; iso-witness = record { fÊ³ = ! R ; inverse = âŠ¥-id T _ ; inverseÊ³ = âŠ¥-id R _ } }



module post-composing-with-bang {C : Category} {T : Initial C} where
  open Category C
  open Initial

  lemma : {X Y : Obj} â†’ (i : X ~> Y) â†’ ! T >~> i â‰¡ ! T
  lemma i = !-unique T (! T >~> i)


Initial-Cat-Preorder : Initial Cat-Preorder
Initial-Cat-Preorder =
  record { ğŸ˜ = Zero , record { _â‰¤_ = Î» _ _ â†’ Zero ; â‰¤-refl = id ; â‰¤-trans = Î» x _ _ _ _ â†’ x ; â‰¤-unique = Î» x _ _ _ â†’ magic x }
         ; ! = (Î» x â†’ magic x) # record { respâ‰¤ = Î» x â†’ magic x }
         ; !-unique = Î» f â†’ proof-irr (extensionality Î» x â†’ magic x)
         }


record Decreasing-Preorder {X} (p : Preorder X) : Set where
  open Preorder
  field
    e : X
    â‰¤-e : (x : X) â†’ _â‰¤_ p e x


Initial-Preorder : âˆ€ {X} â†’ {{p : Preorder X}} (MP : Decreasing-Preorder p) â†’ Initial (PREORDER {{p}})
Initial-Preorder MP = record { ğŸ˜ = e MP ; ! = Î» {x} â†’ â‰¤-e MP x ; !-unique = Î» {x} _ â†’ â‰¤-unique _ x _ _ }
  where open Decreasing-Preorder
